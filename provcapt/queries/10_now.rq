#title:Get information about a trial
#comment:This selects the pieces of general information about a trial that describe it on a very rough detail level, i. e. invoked script, execution command used, start and end time, optionally the id of a parent trial.
#command:now provo --format turtle 7
#tags:noworkflow,alias,optional,bind,coalesce,provo
#color:ex-green

SELECT ?trial_id ?script_file ?command ?start_time ?end_time ?parent_trial
WHERE {
 ?script a prov:SoftwareAgent ;
         ns1:id ?trial_id ;
         ns1:script ?script_file .
  
 ?execution a prov:Activity ;
			prov:qualifiedAssociation ?execAssociation .
  
 ?execAssociation a prov:Association ;
				  prov:agent ?script .
  
 ?execution prov:endedAtTime ?end_time ;
			prov:startedAtTime ?start_time ;
			ns1:nowCommand ?command ;
  
 OPTIONAL { ?execution ns1:parentId ?parent } .

 BIND(COALESCE(?parent, "<none>") AS ?parent_trial) 
}
