#title:Get unused return values
#comment:Selects all entities that are generated by some activity (= output parameter), but are not consumed by any function as input parameter (i. e. are not used in further processing steps).\nSuch variables typically denote overall outputs of workflows.
#command:yw extract script.py -c extract.provenancefile=script_prov -c extract.provenanceformat=turtle -c extract.provenancens=http://yesworkflow.org/ -c extract.provenanceprefix=yw
#tags:yesWorkflow,filter,not,exists,order,alias
#color:ex-yellow

SELECT (?entity AS ?return_value) (?activity AS ?function)
WHERE {
 # retrieve all return values (generated entities)
 ?generation a prov:Generation .
 ?generation prov:activity ?activity .
 ?entity prov:qualifiedGeneration ?generation .

 # filter out those for which there is no usage by some activity
 FILTER NOT EXISTS {
   ?usage a prov:Usage .
   ?usage prov:entity ?entity .
   ?activity2 prov:qualifiedUsage ?usage .
 }

 # optional: exclude certain functions from output computation
 FILTER ( (?activity != yw:inst_s_main) )
 
}
ORDER BY ?entity ?activity
