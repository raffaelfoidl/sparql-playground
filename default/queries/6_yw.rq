#title:Get functions that influenced output parameters
#comment:Selects all functions that are involved in the generation of the output parameters in this script. One result record contains the return value, the number of functions found to be influental and a comma-separated list of those functions. 
#command:yw extract script.py -c extract.provenancefile=script_prov -c extract.provenanceformat=turtle -c extract.provenancens=http://yesworkflow.org/ -c extract.provenanceprefix=yw
#tags:yesWorkflow,alias,bind,replace,str,group,order,property path,group_concat,distinct,filter
#color:ex-yellow

SELECT ?return_value (COUNT(DISTINCT ?function_name) AS ?affected_by_count) (GROUP_CONCAT(DISTINCT ?function_name; separator=", ") AS ?functions)
WHERE { 
 # starting point: ?return_value (could be a hard-coded literal if a specific
 # parameter is of special interest) is generated by some qualified generation
 ?return_value prov:qualifiedGeneration ?generation . 
  
 # from there on, this generation is caused by an activity, which uses
 # entites as parameters which in turn are again generated by some activity.
 # the property path in parenthesis equals a "step from one activity to 
 # the next" (traversing the graph "upwards"), until the last activity is reached
 # (i. e. one that does not have any inputs)
 ?generation (prov:activity/prov:qualifiedUsage/prov:entity/prov:qualifiedGeneration)*/prov:activity ?activity .

 # optional: exclude certain functions from being considered in output computation
 FILTER ( (?activity != yw:inst_m.main) )
 
 # optional: only display local name instead of full function URI
 BIND(replace(str(?activity), ".*\\/", "") AS ?function_name)
}
GROUP BY ?return_value
ORDER BY ?return_value
