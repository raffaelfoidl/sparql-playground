#title:Compare information on two trials
#comment:This selects the pieces of general information about two trials, i. e. invoked script, execution command used, start and end time, optionally the id of a parent trial.\nThese pieces of data are compared between the two trials and a "=>" denotes differences between the two executions.
#command:now diff -p --format turtle 7 1
#tags:noworkflow,alias,coalesce,bind,str,if,optional,limit,filter
#color:ex-green

SELECT ?trial_id ?script_file ?command ?start_time ?end_time ?parent_trial
WHERE {
 # information on first execution
 ?script1 a prov:SoftwareAgent ;
          ns1:id ?trial_id1 ;
          ns1:script ?script_file1 .
  
 ?execution1 a prov:Activity ;
             prov:qualifiedAssociation ?execAssociation1 .
  
 ?execAssociation1 a prov:Association ;
                   prov:agent ?script1 .
  
 ?execution1 prov:endedAtTime ?end_time1 ;
			 prov:startedAtTime ?start_time1 ;
			 ns1:nowCommand ?command1 ;
  
 OPTIONAL { ?execution1 ns1:parentId ?parent1 } .
 BIND(COALESCE(?parent1, "<none>") AS ?parent_trial1) .
 
 # information on second execution
 ?script2 a prov:SoftwareAgent ;
          ns1:id ?trial_id2 ;
          ns1:script ?script_file2 .
  
 ?execution2 a prov:Activity ;
		     prov:qualifiedAssociation ?execAssociation2 .
  
 ?execAssociation2 a prov:Association ;
				   prov:agent ?script2 .
  
 ?execution2 prov:endedAtTime ?end_time2 ;
			 prov:startedAtTime ?start_time2 ;
			 ns1:nowCommand ?command2 ;
  
 OPTIONAL { ?execution2 ns1:parentId ?parent2 } .

 BIND(COALESCE(?parent2, "<none>") AS ?parent_trial2) .
 
 # bind value (if same), otherwise show difference
 BIND( IF(?trial_id1 = ?trial_id2, str(?trial_id1), CONCAT(str(?trial_id1), " => ", str(?trial_id2))) AS ?trial_id) .
 BIND( IF(?script_file1 = ?script_file2, ?script_file1, CONCAT(?script_file1, " => ", ?script_file2)) AS ?script_file) .
 BIND( IF(?command1 = ?command2, ?command1, CONCAT(?command1, " => ", ?command2)) AS ?command) .
 BIND( IF(?start_time1 = ?start_time2, str(?start_time1), CONCAT(str(?start_time1), " => ", str(?start_time2))) AS ?start_time) .
 BIND( IF(?end_time1 = ?end_time2, str(?end_time1), CONCAT(str(?end_time1), " => ", str(?end_time2))) AS ?end_time) .
 BIND( IF(str(?parent_trial1) = str(?parent_trial2), str(?parent_trial1), CONCAT(str(?parent_trial1), " => ", str(?parent_trial2))) AS ?parent_trial) .
 
 FILTER (?trial_id1 != ?trial_id2) # only the two trial executions are interesting for comparison
}
LIMIT 1 # both resulting rows contain same amount of information (symmetry)
