#title:Get parameters that were not generated
#comment:Selects all entities that are consumed by functions (= input parameters), but were not generated by any activity. This typically holds true for overall parameters to the workflow which are provided by the person executing a script. In addition to the parameter names, their descriptions retrieved by the yesWorkflow parser are presented, if available. Finally, the number of functions consuming the respective parameters and a sample description are displayed (i. e. if there are multiple differing descriptions, one of them is arbitrarily chosen for display by the SPARQL engine).
#command:yw extract script.py -c extract.provenancefile=script_prov -c extract.provenanceformat=turtle -c extract.provenancens=http://yesworkflow.org/ -c extract.provenanceprefix=yw
#tags:yesWorkflow,alias,group,optional,coalesce,bind,filter,not,exists
#color:ex-yellow

SELECT (?entity AS ?input_parameter) (SAMPLE(?description) AS ?description_sample) (COUNT(?activity) AS ?usage_count) 
WHERE {
 # retrieve all input parameters
 ?usage a prov:Usage .
 ?usage prov:entity ?entity .
 ?activity prov:qualifiedUsage ?usage .
 OPTIONAL { ?entity rdfs:label ?label } .

 BIND(COALESCE(?label, "<undefined>") AS ?description)
 
 # filter out parameters for which were not generated by some activity
 FILTER NOT EXISTS {  
   ?generation a prov:Generation .
   ?generation prov:activity ?activity2 .
   ?entity prov:qualifiedGeneration ?generation .
 } 

 # filter out function calls
 FILTER NOT EXISTS {
   ?entity a "calledFunction" .
 }
 
}
GROUP BY ?entity ?description
